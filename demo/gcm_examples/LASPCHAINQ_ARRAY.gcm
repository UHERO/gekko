// Danner Kædet Laspeyres mængdeindeks og paascheprisindeks for kvartalsserier.
// Aggregatet dannes for den længst mulige periode. Periode og frekvens sættes afslutnngsvis som før kald af funktionen
// INPUT    : Liste over prisserier, liste over mængdeserier (foranstillet "'-" indebærer at serien trækkes ud af indeks), referenceår: (pris = 1)
// OUTPUT   : MAP der indeholder følgende (series value, series quantity, series price, series price_lag).

FUNCTION MAP  LASPCHAINQ_ARRAY(list #array_p, list #array_q, date %refaar);
	OPTION freq q;
	date   %current_start = currentPerStart() ;
	date   %current_end   = currentPerEnd()   ;
	string %current_freq  = currentFreq()     ;
	#q = list(); #p = list();
	for val %i = 1 to #array_q.length();
		%ss = #array_q[%i].startswith( '-' );
		if ( %ss == 1 );
				list #q = #q.append('-qq{%i}');
				STRING %nn = #array_q[%i].substring(2,length(#array_q[%i])-1);
				series work:qq{%i} = {%nn};
			else;
				list #q = #q.append('qq{%i}');
				series work:qq{%i} = {#array_q[%i]} ;
		end;
	end;
	for val %i = 1 to #array_p.length();
		series work:pp{%i} = {#array_p[%i]};
		list #p = #p.append('pp{%i}');
	end;
  date %st = {#p[1]}.fromseries('DataStart');
  date %sl = {#p[1]}.fromseries('DataEnd');
  for string %i = #p + #q;
	%ss = %i.startswith( '-' );
		if ( %ss == 1 );
			%ii = %i.substring(2,%i.length()-1) ;
		 else;
			string %ii = %i;
		end;
		date %st_temp = %ii.fromSeries( 'DataStart' );
		if ( %st_temp > %st );
			date %st = %st_temp;
		end;
		date %sl_temp = %ii.fromSeries( 'DataEnd' );
		if ( %sl_temp < %sl );
			date %sl = %sl_temp;
		end;
  end;
  time %st %sl;
  option freq a;
  for string %p = #p string %q = #q;
		%ss = %q.startswith( '-' );
		if ( %ss == 1 );
			%qq = %q.substring(2,%q.length()-1) ;
		 else;
			string %qq = %q;
		end;
	  collapse {%qq}!a = {%qq}!q avg;
	  option freq q;
	  series work:v_{%p} = {%p} * {%qq};
	  option freq a;
      collapse v_{%p}!a = v_{%p}!q avg;	  
	  series  work:{%p}    = v_{%p} / {%qq} ; FINDMISSINGDATA <replace = 0> {%p};  //Missing data opstår her når mængden bliver nul - prisen sættes derefter ligeledes til nul taget fra monagenr!
  	  series  work:{%p}_la = {%p}[-1]; 
  end;
  #LPAGG = laspchain(#p, #q, %refaar);
   work:p_la  = #LPAGG.p[-1];
   option freq q;
  INTERPOLATE work:p_la!q = work:p_la!a repeat;
  series work:value    = 0 ;
  series work:quantity = 0 ;
  for string %p = #p string %q = #q;
	 interpolate work:{%p}_la!q = work:{%p}_la!a repeat;
	 %ss = %q.startswith( '-' );
     	if ( %ss == 1 );
			%qq = %q.substring(2,%q.length()-1);
			VAL %fortegn = -1;
		 else;
			string %qq = %q;
			VAL %fortegn =  1;
		end;
  	 series work:value 		= value    + {%p}    * %fortegn * {%qq};
	 series work:quantity	= quantity + {%p}_la * %fortegn * {%qq}/p_la;  
  end;
  series work:value_la = quantity*p_la;
  MAP work:#out_map = ( series work:value = value , series work:quantity = quantity , series work:price = value/quantity , series work:price_lag = p_la, series work:value_lag = value_la );
  time %current_start %current_end;
  option freq {%current_freq};
  RETURN  #out_map;
END;