// EXPERIMENTING WITH USSOL
// SEE ALSO THE BUG LIST IN A SEPARATE FILE

// SET FONT SIZE IN GEKKO
OPTION INTERFACE ZOOM = 120;
// CLOSE BANKS AND CLEAR MEMORY
RESTART;
// SET WORKING DIRECTORY, AND SUBFOLDERS 
// (IF YOU USE SINGLE QUOTES, HAVE TO PROVIDE ABSOLUTE PATH)
%WORK_DIR = 'I:\FORECAST\GEKKO';
OPTION FOLDER WORKING = %WORK_DIR;
OPTION FOLDER COMMAND =  '{%WORK_DIR}\CMD';
OPTION FOLDER BANK = '{%WORK_DIR}\DATA';
// WILL DO MOSTLY DATA MANIPULATION
MODE DATA;

// LOAD FUNCTIONS
RUN LIB.GCM;

// SET UP PERIODS ETC.
%BNK_START = 1950;
%BNK_END = 2060;

// LOAD RELEVANT DATA (NEED TO REPLACE @ WITH _ IN TSD FILES)
#TSD_LIST = ('US_Q', 'US_A');
FOR STRING %I = #TSD_LIST;
IMPORT <ALL TSD> '\DATA\{%I}.TSD';
END;

// MAKE COPIES OF LOADED SERIES FOR STORING FORECASTS
// INDEX ALL SERIES AND SAVE DATA IN THE BANK EXT_DAT.GBK
COPY *!A TO *_SOLA!A;
COPY *!Q TO *_SOLQ!Q;
INDEX *:*!* TO #EXT_LIST;
PRT {#EXT_LIST};
WRITE {#EXT_LIST} FILE=EXT_DAT;

// USE POPULATION ESTIMATES AND PROJECTIONS FROM CENSUS
// HTTPS://WWW2.CENSUS.GOV/PROGRAMS-SURVEYS/POPPROJ/DATASETS/2017/2017-POPPROJ/NP2017_D1.CSV
// HTTPS://WWW2.CENSUS.GOV/PROGRAMS-SURVEYS/POPEST/DATASETS/2010-2017/NATIONAL/ASRH/NC-EST2017-AGESEX-RES.CSV
// FILL SERIES WITH CENSUS PROJECTIONS, AND FILL IN RECENT (5 YEAR) HISTORY SO THAT GROWTH RATES AND SPLINES CAN BE CALCULATED
OPTION FREQ=A;
TIME 2016 2060;
NCEN_US_SOLA = 323127513,325511184,327891911,330268840,332639102,334998398,337341954,339665118,341963408,344234377,346481182,348695115,350872007,353008224,355100730,357147329,359146709,361098559,363003410,364862145,366676312,368447857,370178704,371871238,373527973,375151805,376746115,378314343,379860859,381390297,382907447,384415207,385917628,387418788,388922201,390430803,391947055,393472783,395009307,396557404,398117875,399690963,401276590,402874337,404483055; 			
NCEN_US_SOLA /= 1000; 	// SCALE BY 1000

// INTERPOLATE ANNUAL DATA TO QUARTERLY (FUNCTION DEFINED IN LIB.GCM)
N_US_SOLQTEMP!Q = INTERP_QA_LIN(NCEN_US_SOLA);
COLLAPSE N_US_SOLQTEMP!A = N_US_SOLQTEMP!Q AVG; // FOR STOCK TYPE VARIABLES AVG, FOR FLOW TYPE VARIABLES TOTAL
PRT N_US_SOLQTEMP!Q, N_US_SOLQTEMP!A, NCEN_US_SOLA;

// SWITCH TO QUARTERLY FREQUENCY
OPTION FREQ=Q;

// ADDFACTOR FOR LEVEL (TRANSITION FROM HISTORY TO FORECAST)
// FIND THE LAST VALUE IN HISTORY, CREATE AN ADDFACTOR WITH THE DIFFERENCE AND ADJUST LEVEL OF N_US_SOLQ
%DAT_END = FROMSERIES(N_US, 'DATAEND');
%N_US_SOLQADDIF = N_US[%DAT_END] - N_US_SOLQTEMP[%DAT_END];
N_US_SOLQ <%DAT_END %BNK_END> = N_US_SOLQTEMP + %N_US_SOLQADDIF;

// COMPARE GROWTH RATES IN HISTORY AND FORECAST
PRT <P> N_US, N_US_SOLQ;

// ADDFACTOR FOR GROWTH
N_US_SOLQPCHAADD = 0;
N_US_SOLQPCHAADD <%DAT_END+1 %DAT_END+80> = M();
N_US_SOLQPCHAADD <%DAT_END+1 %DAT_END+4> = -0.05;
SMOOTH N_US_SOLQPCHAADD = N_US_SOLQPCHAADD LINEAR;

// EXTEND HISTORY USING GROWTH RATE
N_US_SOLQ <%DAT_END+1 %BNK_END> %= (PCH(N_US_SOLQ)+N_US_SOLQPCHAADD);

// PRINT RESULTS
PRT N_US_SOLQPCHAADD, PCH(N_US_SOLQ)+N_US_SOLQPCHAADD, N_US_SOLQ, N_US_SOLQTEMP;
PRT %N_US_SOLQADDIF;

// DELETE TEMPORARY SERIES
DELETE NCEN_US_SOLA!A, N_US_SOLQTEMP, N_US_SOLQTEMP!A, %N_US_SOLQADDIF, N_US_SOLQPCHAADD;

// AGGREGATE TO ANNUAL FREQUENCY
COLLAPSE N_US_SOLA!A = N_US_SOLQ!Q AVG; // FOR STOCK TYPE VARIABLES AVG, FOR FLOW TYPE VARIABLES TOTAL
PRT N_US_SOLA!A, N_US_SOLQ!Q;

// PRODUCE A BASIC PLOT OF RESULTS (ATTRIBUTES IN '' ARE CASE SENSITIVE!)
PLOT <2015 2030 TYPE=LINES> N_US<DASHTYPE='1' LINECOLOR='blue'>, N_US_SOLQ<DASHTYPE='2' LINECOLOR='red'>;
PRT N_US, N_US_SOLQ;

// TO DO: REVISE WITH YOY GROWTH, FUNCTION FOR CAR, ETC.
// CONTINUE WITH THE FORECAST WORKFLOW.

