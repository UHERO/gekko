// EXPERIMENTING WITH USSOL
// SEE ALSO THE BUG LIST IN A SEPARATE FILE

// SET FONT SIZE IN GEKKO
OPTION INTERFACE ZOOM = 120;
// CLOSE BANKS AND CLEAR MEMORY
RESTART;
// SET WORKING DIRECTORY, AND SUBFOLDERS 
// (IF YOU USE SINGLE QUOTES, HAVE TO PROVIDE ABSOLUTE PATH)
%WORK_DIR = 'I:\FORECAST\GEKKO';
OPTION FOLDER WORKING = %WORK_DIR;
OPTION FOLDER COMMAND =  '{%WORK_DIR}\CMD';
OPTION FOLDER BANK = '{%WORK_DIR}\DATA';
// WILL DO MOSTLY DATA MANIPULATION
MODE DATA;

// LOAD FUNCTIONS
RUN LIB.GCM;

// SET UP PERIODS ETC.
%BNK_START = 1950;
%BNK_END = 2060;
%CURR_YR = CURRENTYEAR();
TIME %BNK_START %BNK_END;

// LOAD RELEVANT DATA (NEED TO REPLACE @ WITH _ IN TSD FILES)
#TSD_LIST = ('US_Q', 'US_A');
FOR STRING %I = #TSD_LIST;
IMPORT <ALL TSD> '\DATA\{%I}.TSD';
END;

// MAKE COPIES OF LOADED SERIES FOR STORING FORECASTS
// INDEX ALL SERIES AND SAVE DATA IN THE BANK EXT_DAT.GBK
COPY *!A TO *_SOLA!A;
COPY *!Q TO *_SOLQ!Q;
INDEX *:*!* TO #EXT_LIST;
DISP {#EXT_LIST};
WRITE {#EXT_LIST} FILE=EXT_DAT;

// POPULATION

// USE POPULATION ESTIMATES AND PROJECTIONS FROM CENSUS
// HTTPS://WWW2.CENSUS.GOV/PROGRAMS-SURVEYS/POPPROJ/DATASETS/2017/2017-POPPROJ/NP2017_D1.CSV
// HTTPS://WWW2.CENSUS.GOV/PROGRAMS-SURVEYS/POPEST/DATASETS/2010-2017/NATIONAL/ASRH/NC-EST2017-AGESEX-RES.CSV
// FILL SERIES WITH CENSUS PROJECTIONS, AND FILL IN RECENT (5 YEAR) HISTORY SO THAT GROWTH RATES AND SPLINES CAN BE CALCULATED
OPTION FREQ=A;
NCEN_US_SOLA <2016 2060> = 323127513,325511184,327891911,330268840,332639102,334998398,337341954,339665118,341963408,344234377,346481182,348695115,350872007,353008224,355100730,357147329,359146709,361098559,363003410,364862145,366676312,368447857,370178704,371871238,373527973,375151805,376746115,378314343,379860859,381390297,382907447,384415207,385917628,387418788,388922201,390430803,391947055,393472783,395009307,396557404,398117875,399690963,401276590,402874337,404483055; 			
NCEN_US_SOLA /= 1000; 	// SCALE BY 1000

// INTERPOLATE ANNUAL DATA TO QUARTERLY (FUNCTION DEFINED IN LIB.GCM)
NCEN_US_SOLQ!Q = INTERP_QA_LIN(NCEN_US_SOLA);
COLLAPSE NCEN_US_SOLA1!A = NCEN_US_SOLQ!Q AVG; // FOR STOCK TYPE VARIABLES AVG, FOR FLOW TYPE VARIABLES TOTAL
PRT <%CURR_YR-2 %CURR_YR+3> NCEN_US_SOLQ!Q, NCEN_US_SOLA1!A, NCEN_US_SOLA;

// SWITCH TO QUARTERLY FREQUENCY
OPTION FREQ=Q;

// ADDFACTOR FOR LEVEL (TRANSITION FROM HISTORY TO FORECAST)
// FIND THE LAST VALUE IN HISTORY, CREATE AN ADDFACTOR WITH THE DIFFERENCE AND ADJUST LEVEL
%DAT_END = FROMSERIES(N_US, 'DATAEND');
%N_US_SOLQ_ADDLEV = N_US[%DAT_END] - NCEN_US_SOLQ[%DAT_END];
N_US_SOLQ <%DAT_END %BNK_END> = NCEN_US_SOLQ + %N_US_SOLQ_ADDLEV;

// COMPARE GROWTH RATES IN HISTORY AND FORECAST
PRT <%CURR_YR-2 %CURR_YR+3> N_US, N_US_SOLQ;

// ADDFACTOR FOR GROWTH
N_US_SOLQ_ADDGRO = 0;
N_US_SOLQ_ADDGRO <%DAT_END+1 %DAT_END+80> = M();
N_US_SOLQ_ADDGRO <%DAT_END+1 %DAT_END+4> = -0.25;
SMOOTH N_US_SOLQ_ADDGRO = N_US_SOLQ_ADDGRO LINEAR;

// EXTEND HISTORY USING GROWTH RATE (FUNCTIONS DEFINED IN LIB.GCM)
N_US_SOLQ_GRO = PCHA(N_US_SOLQ)+N_US_SOLQ_ADDGRO;
N_US_SOLQ_GRO = PCHA_TO_PCH(N_US_SOLQ_GRO);
N_US_SOLQ <%DAT_END+1 %BNK_END> %= N_US_SOLQ_GRO;

// PRINT RESULTS
PRT <%CURR_YR-2 %CURR_YR+3> N_US_SOLQ_GRO, N_US_SOLQ_ADDGRO, PCHA(N_US_SOLQ), PCHY(N_US_SOLQ), N_US_SOLQ, NCEN_US_SOLQ!Q;
PRT %N_US_SOLQ_ADDLEV;

// AGGREGATE TO ANNUAL FREQUENCY
COLLAPSE N_US_SOLA!A = N_US_SOLQ!Q AVG; // FOR STOCK TYPE VARIABLES AVG, FOR FLOW TYPE VARIABLES TOTAL
PRT <%CURR_YR-2 %CURR_YR+3> N_US_SOLA!A, N_US_SOLQ!Q, N_US!A, N_US!Q;

// PRODUCE A BASIC PLOT OF RESULTS (ATTRIBUTES IN '' ARE CASE SENSITIVE!)
PLOT <%CURR_YR-2 %CURR_YR+10 TYPE=LINES> N_US<DASHTYPE='1' LINECOLOR='blue'>, N_US_SOLQ<DASHTYPE='2' LINECOLOR='red'>;

// DELETE TEMPORARY SERIES
INDEX *:*!*;
DELETE NCEN_US_SOLA1!A, %N_US_SOLQ_ADDLEV, N_US_SOLQ_GRO, N_US_SOLQ_ADDGRO;

// 16+ POPULATION

OPTION FREQ=A;	
// 	GET THE 16+ US POPULATION PROJECTIONS FROM CENSUS:
// 	HTTPS://WWW2.CENSUS.GOV/PROGRAMS-SURVEYS/POPPROJ/DATASETS/2017/2017-POPPROJ/NP2017_D1.CSV
N16_US_SOLA <2016 2060> = 257955453,260310555,262550200,264792059,267049286,269282525,271511472,273756663,275962588,278018690,280054931,282067427,284005393,285905941,287783531,289630116,291415785,293243240,295043303,296814180,298553605,300259159,301929123,303562429,305158790,306719083,308245025,309738427,311201577,312637235,314048806,315438493,316810301,318168975,319519290,320865864,322213035,323564510,324923129,326291095,327670395,329062335,330468057,331888253,333322800;

// 	ADD/UPDATE RECENT HISTORY (NEED FOR SPLINES):
// 	HTTPS://WWW2.CENSUS.GOV/PROGRAMS-SURVEYS/POPEST/DATASETS/2010-2017/NATIONAL/ASRH/NC-EST2017-AGESEX-RES.CSV
N16_US <2010 2017> = 243906873,246315892,248735191,251016036,253402603,255788180,258228041,260583066;

// ADDFACTOR PROJECTION BY AVERAGE DIFFERENCE DURING THE OVERLAP (2016-17). 
// THE DIFFERENCE IS (272,588+272,511)/2 = 272,550
N16_US_SOLA += 272550;
N16_US_SOLA <2010 2017> = N16_US; // FILL IN RECENT HISTORY
N16_US_SOLA = N16_US_SOLA/1000; // SCALE BY 1000

// THE DENOMINATOR FOR THE LFP CALCULATION SHOULD BE NON-INSTITUTIONAL 16+ (HTTP://WWW.BLS.GOV/WEB/EMPSIT/CPSEEA01.HTM)
// THE DIFFERENCE BETWEEN 16+ AND NON-INSTITUTIONAL 16+ IS ABOUT 5.5 MILLION (CHECK THIS)
// VARIABLE FOR ACTUAL NON-INSTITUTIONAL 16+ HISTORY
N16NI_US_A <2010 2018> = 237829,239618,243284,245679,247947,250801,253538,255079,257791;	
PRINT <%CURR_YR-2 %CURR_YR+3> PCH(N16_US_SOLA), PCH(N16NI_US_A), N16_US_SOLA, N16NI_US_A, N16_US_SOLA-N16NI_US_A; // CHECK THE DIFFERENCE, SHOULD BE CLOSE TO 5.5 MILLION 
N16NI_US_SOLA = N16NI_US_A;

// ADDFACTOR FOR LEVEL (TRANSITION FROM HISTORY TO FORECAST)
// FIND THE LAST VALUE IN HISTORY, CREATE AN ADDFACTOR WITH THE DIFFERENCE AND ADJUST LEVEL
%DAT_END = FROMSERIES(N16NI_US_A, 'DATAEND');
%N16_US_SOLA_ADDLEV = N16NI_US_A[%DAT_END] - N16_US_SOLA[%DAT_END];
N16NI_US_SOLA <%DAT_END %BNK_END> = N16_US_SOLA + %N16_US_SOLA_ADDLEV;

// COMPARE GROWTH RATES IN HISTORY AND FORECAST
PRT <%CURR_YR-2 %CURR_YR+3> N16NI_US_A, N16NI_US_SOLA;

// ADDFACTOR FOR GROWTH
N16NI_US_SOLA_ADDGRO = 0;
N16NI_US_SOLA_ADDGRO <%DAT_END+1 %DAT_END+20> = M();
N16NI_US_SOLA_ADDGRO <%DAT_END+1 %DAT_END+1> = 0;
SMOOTH N16NI_US_SOLA_ADDGRO = N16NI_US_SOLA_ADDGRO LINEAR;

// EXTEND HISTORY USING GROWTH RATE
N16NI_US_SOLA_GRO = PCH(N16NI_US_SOLA) + N16NI_US_SOLA_ADDGRO;
N16NI_US_SOLA <%DAT_END+1 %BNK_END> %= N16NI_US_SOLA_GRO;

// PRINT RESULTS
PRT <%CURR_YR-2 %CURR_YR+3> N16NI_US_SOLA_GRO, N16NI_US_SOLA_ADDGRO, PCH(N16NI_US_SOLA), N16NI_US_A;
PRT %N16_US_SOLA_ADDLEV;

// INTERPOLATE ANNUAL DATA TO QUARTERLY
N16NI_US_SOLQ!Q = INTERP_QA_LIN(N16NI_US_SOLA);
COLLAPSE N16NI_US_SOLA1!A = N16NI_US_SOLQ!Q AVG; // FOR STOCK TYPE VARIABLES AVG, FOR FLOW TYPE VARIABLES TOTAL
PRT <%CURR_YR-2 %CURR_YR+3> N16NI_US_SOLQ!Q, N16NI_US_SOLA1!A, N16NI_US_SOLA, N16_US;

// PRODUCE A BASIC PLOT OF RESULTS (ATTRIBUTES IN '' ARE CASE SENSITIVE!)
PLOT <%CURR_YR-2 %CURR_YR+10 TYPE=LINES> N16_US<DASHTYPE='1' LINECOLOR='blue'>, N16NI_US_SOLA<DASHTYPE='2' LINECOLOR='red'>;

// DELETE TEMPORARY SERIES
INDEX *:*!*;
DELETE N16NI_US_SOLA1!A, %N16_US_SOLA_ADDLEV, N16NI_US_SOLA_GRO, N16NI_US_SOLA_ADDGRO;
