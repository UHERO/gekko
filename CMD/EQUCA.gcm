// *************************
// COUNTY ALLOCATION EQUATIONS
// AUTHOR: PETER FULEKY
// DATE: 2020-06-12
// NOTES:
// RUN THE WHOLE SCRIPT FROM THE COMMAND LINE USING RUN EQUCA.GCM;
// OR CALL IT FROM THE MAIN COUNTY ALLOCATION FILE
// THIS IS THE SECOND STAGE TRIGGERED BY THE REDISTRIBUTE SWITCH IN THE MAIN FILE 
// 	1. CONSTRUCT EQUATIONS
// 	2. REDISTRIBUTE RESIDUAL
// *************************




// STAGE 1. CONSTRUCT EQUATIONS
// CALCULATE COEFFICIENT VALUES AND STORE EQUATIONS IN 'CNTY_EQ.FRM'
// IF (%REDISTRIBUTE<>'Y');

// SAVE EQUATIONS INTO FORMULA FILE?
IF (%SAVE_EQ=='Y');  
PIPE 'CNTY_EQ.FRM';
END;
PIPE <PAUSE>;

// WILL DO MOSTLY DATA MANIPULATION
MODE DATA;


TELL '*************************';
TELL ' JOBS BY SECTOR          ';
TELL '*************************';



// OPTIONAL: TWEAK THE_SHARES OF COUNTIES
// BEST TO BE LEFT AT 1
MAU_BALANC = 1 REP *;
HAW_BALANC = 1 REP *;
KAU_BALANC = 1 REP *;



// LOOP OVER ALL INDUSTRIES
FOR STRING %SER_I = CT, MN, _TRADE, _TU, _FIR, AF, HC, GVFD, _GVSL;
// %SER_I = 'ECT';


// LOOP OVER ALL COUNTIES
FOR STRING %CNTY_I = HAW, KAU, MAU;
// %CNTY_I = 'HAW';


// GET THE LAST PERIOD IN HISTORY
%LAST_PER = FROMSERIES(E{%SER_I}_{%CNTY_I}, 'DATAEND');

// CALCULATE THE RELATIVE IMPORTANCE (SHARE) OF THE INDUSTRY
E{%SER_I}_{%CNTY_I}_SH <%LAST_PER %LAST_PER> = {%CNTY_I}_BALANC*((E{%SER_I}_{%CNTY_I}/E_NF_{%CNTY_I})/(E{%SER_I}_NBI/E_NF_NBI)) / 
  (((E{%SER_I}_HAW/E_NF_HAW)/(E{%SER_I}_NBI/E_NF_NBI) + 
	(E{%SER_I}_KAU/E_NF_KAU)/(E{%SER_I}_NBI/E_NF_NBI) + 
	(E{%SER_I}_MAU/E_NF_MAU)/(E{%SER_I}_NBI/E_NF_NBI)) / 3);

// GET THE VALUE OF THE SHARE IN THE LAST AVAILABLE YEAR
%E{%SER_I}_{%CNTY_I}_SH = E{%SER_I}_{%CNTY_I}_SH[%LAST_PER];

// CALCULATE THE DIFFERENCE BETWEEN HISTORY AND FORECAST
E{%SER_I}_{%CNTY_I}_ADD <%LAST_PER %LAST_PER> = E{%SER_I}_{%CNTY_I} - %E{%SER_I}_{%CNTY_I}_SH*E_NF_{%CNTY_I}*(E{%SER_I}_NBI/E_NF_NBI);

// GET THE DIFFERENCE IN THE LAST AVAILABLE YEAR
%E{%SER_I}_{%CNTY_I}_ADD = E{%SER_I}_{%CNTY_I}_ADD[%LAST_PER];

// SAVE THE EQUATION WITH PLUGGED IN COEFFICIENTS
PIPE <CONTINUE>;
TELL 'FRML _IJD E{%SER_I}_{%CNTY_I} = ' + %E{%SER_I}_{%CNTY_I}_ADD + ' + ' + %E{%SER_I}_{%CNTY_I}_SH + '*E_NF_{%CNTY_I}*(E{%SER_I}_NBI/E_NF_NBI);';
PIPE <PAUSE>;

// DELETE TERMPORARY VARIABLES
DELETE E{%SER_I}_{%CNTY_I}_SH, E{%SER_I}_{%CNTY_I}_ADD;


END; // END FOR LOOP


END; // END FOR LOOP



// LOOP OVER ALL COUNTIES FOR AGGREGATING IDENTITIES
FOR STRING %CNTY_I = HAW, KAU, MAU;
// %CNTY_I = 'HAW';


// E_ELSE


// SAVE THE EQUATION
PIPE <CONTINUE>;
TELL 'FRML _I E_ELSE_{%CNTY_I} = E_NF_{%CNTY_I}-ECT_{%CNTY_I}-EMN_{%CNTY_I}-E_TU_{%CNTY_I}-E_TRADE_{%CNTY_I}-E_FIR_{%CNTY_I}-EAF_{%CNTY_I}-EHC_{%CNTY_I}-EGV_{%CNTY_I};';
PIPE <PAUSE>;


// E_SV


// SAVE THE EQUATION
PIPE <CONTINUE>;
TELL 'FRML _I E_SV_{%CNTY_I} = EAF_{%CNTY_I}+EHC_{%CNTY_I}+E_ELSE_{%CNTY_I};';
PIPE <PAUSE>;


// EGV


// SAVE THE EQUATION
PIPE <CONTINUE>;
TELL 'FRML _I EGV_{%CNTY_I} = EGVFD_{%CNTY_I}+E_GVSL_{%CNTY_I};';
PIPE <PAUSE>;


END; // END FOR LOOP




TELL '*************************';
TELL ' INCOME BY SECTOR        ';
TELL '*************************';



// SET YLCT GROWTH TO THE SAME AS YL_CTMI
PIPE <CONTINUE>;
TELL 'FRML _I DLOG(YLCT_R_NBI) = DLOG(YL_CTMI_R_NBI);';
PIPE <PAUSE>;


// THESE EXIST FOR EACH COUNTY
// LOOP OVER ALL INDUSTRIES
FOR STRING %SER_I = CT, MN, AF, HC, _ELSE, GVFD, _GVSL;
// %SER_I = 'ECT';


// LOOP OVER ALL COUNTIES FOR AGGREGATING IDENTITIES
FOR STRING %CNTY_I = HAW, KAU, MAU;
// %CNTY_I = 'HAW';


// GET THE LAST PERIOD IN HISTORY
%LAST_PER = FROMSERIES(YL{%SER_I}_R_{%CNTY_I}, 'DATAEND');

// CALCULATE THE DIFFERENCE BETWEEN HISTORY AND FORECAST
YL{%SER_I}_R_{%CNTY_I}_ADD <%LAST_PER %LAST_PER> = YL{%SER_I}_R_{%CNTY_I} - YL{%SER_I}_R_NBI*(E{%SER_I}_{%CNTY_I}/(E{%SER_I}_HAW+E{%SER_I}_KAU+E{%SER_I}_MAU));

// GET THE DIFFERENCE IN THE LAST AVAILABLE YEAR
%YL{%SER_I}_R_{%CNTY_I}_ADD = YL{%SER_I}_R_{%CNTY_I}_ADD[%LAST_PER];

// SAVE THE EQUATION
PIPE <CONTINUE>;
TELL 'FRML _I YL{%SER_I}_R_{%CNTY_I} = ' + %YL{%SER_I}_R_{%CNTY_I}_ADD + ' + YL{%SER_I}_R_NBI*(E{%SER_I}_{%CNTY_I}/(E{%SER_I}_HAW+E{%SER_I}_KAU+E{%SER_I}_MAU));';
TELL 'FRML _I YL{%SER_I}_{%CNTY_I} = YL{%SER_I}_R_{%CNTY_I}*CPI_HON/100;';
PIPE <PAUSE>;

// DELETE TERMPORARY VARIABLES
DELETE YL{%SER_I}_R_{%CNTY_I}_ADD;


END; // END FOR LOOP


END; // END FOR LOOP




// THESE ONLY EXIST FOR MAU
// LOOP OVER ALL INDUSTRIES
FOR STRING %SER_I = _TRADE, _TU, _FIR;  
// %SER_I = 'ECT';


// LOOP OVER ALL COUNTIES FOR AGGREGATING IDENTITIES
FOR STRING %CNTY_I = MAU, ;
// %CNTY_I = 'HAW';


// GET THE LAST PERIOD IN HISTORY
%LAST_PER = FROMSERIES(YL{%SER_I}_R_{%CNTY_I}, 'DATAEND');

// CALCULATE THE DIFFERENCE BETWEEN HISTORY AND FORECAST
YL{%SER_I}_R_{%CNTY_I}_ADD <%LAST_PER %LAST_PER> = YL{%SER_I}_R_{%CNTY_I} - YL{%SER_I}_R_NBI*(E{%SER_I}_{%CNTY_I}/(E{%SER_I}_HAW+E{%SER_I}_KAU+E{%SER_I}_MAU));

// GET THE DIFFERENCE IN THE LAST AVAILABLE YEAR
%YL{%SER_I}_R_{%CNTY_I}_ADD = YL{%SER_I}_R_{%CNTY_I}_ADD[%LAST_PER];

// SAVE THE EQUATION
PIPE <CONTINUE>;
TELL 'FRML _I YL{%SER_I}_R_{%CNTY_I} = ' + %YL{%SER_I}_R_{%CNTY_I}_ADD + ' + YL{%SER_I}_R_NBI*(E{%SER_I}_{%CNTY_I}/(E{%SER_I}_HAW+E{%SER_I}_KAU+E{%SER_I}_MAU));';
TELL 'FRML _I YL{%SER_I}_{%CNTY_I} = YL{%SER_I}_R_{%CNTY_I}*CPI_HON/100;';
PIPE <PAUSE>;

// DELETE TERMPORARY VARIABLES
DELETE YL{%SER_I}_R_{%CNTY_I}_ADD;


END; // END FOR LOOP


END; // END FOR LOOP




// LOOP OVER ALL COUNTIES FOR AGGREGATIONS OR ATYPICAL IDENTITIES
FOR STRING %CNTY_I = HAW, KAU, MAU;
// %CNTY_I = 'HAW';


// YL_CTMI_R


// CREATE YL_CTMI_R_{%CNTY_I}, YL_CTMI_{%CNTY_I};

// PIPE <CONTINUE>;
// TELL 'FRML _I YL_CTMI_R_{%CNTY_I} = YLCT_R_{%CNTY_I};';
// TELL 'FRML _I YL_CTMI_{%CNTY_I} = YLCT_{%CNTY_I}*CPI_HON/100;';
// PIPE <PAUSE>;


// YL_SV_R


// SAVE THE EQUATION
PIPE <CONTINUE>;
TELL 'FRML _I YL_SV_R_{%CNTY_I} = YLHC_R_{%CNTY_I}+YLAF_R_{%CNTY_I}+YL_ELSE_R_{%CNTY_I};';
TELL 'FRML _I YL_SV_{%CNTY_I} = YL_SV_R_{%CNTY_I}*CPI_HON/100;';
PIPE <PAUSE>;


// YLGVML


// GET THE LAST PERIOD IN HISTORY
%LAST_PER = FROMSERIES(YLGVML_R_{%CNTY_I}, 'DATAEND');

// CALCULATE THE DIFFERENCE BETWEEN HISTORY AND FORECAST
YLGVML_R_{%CNTY_I}_ADD <%LAST_PER %LAST_PER> = YLGVML_R_{%CNTY_I} - YLGVML_R_NBI*(EGVFD_{%CNTY_I}/(EGVFD_HAW+EGVFD_KAU+EGVFD_MAU));

// GET THE DIFFERENCE IN THE LAST AVAILABLE YEAR
%YLGVML_R_{%CNTY_I}_ADD = YLGVML_R_{%CNTY_I}_ADD[%LAST_PER];

// SAVE THE EQUATION
PIPE <CONTINUE>;
TELL 'FRML _I YLGVML_R_{%CNTY_I} = ' + %YLGVML_R_{%CNTY_I}_ADD + ' + YLGVML_R_NBI*(EGVFD_{%CNTY_I}/(EGVFD_HAW+EGVFD_KAU+EGVFD_MAU));';
TELL 'FRML _I YLGVML_{%CNTY_I} = YLGVML_R_{%CNTY_I}*CPI_HON/100;';
PIPE <PAUSE>;

// DELETE TERMPORARY VARIABLES
DELETE YLGVML_R_{%CNTY_I}_ADD;


// YLGV


// SAVE THE EQUATION
PIPE <CONTINUE>;
TELL 'FRML _I YLGV_R_{%CNTY_I} = YLGVFD_R_{%CNTY_I}+YL_GVSL_R_{%CNTY_I}+YLGVML_R_{%CNTY_I};';
TELL 'FRML _I YLGV_{%CNTY_I} = YLGV_R_{%CNTY_I}*CPI_HON/100;';
PIPE <PAUSE>;


END; // END FOR LOOP



TELL '*************************';
TELL ' END OF EQUATONS         ';
TELL '*************************';


PIPE <CONTINUE>;
IF (%SAVE_EQ=='Y');  
PIPE <STOP>;
END;



// END; // END IF





