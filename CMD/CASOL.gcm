// *************************
// COUNTY ALLOCATION MAIN FILE
// AUTHOR: PETER FULEKY
// DATE: 2020-06-14
// NOTES:
// RUN THE WHOLE SCRIPT FROM THE COMMAND LINE USING RUN CASOL.GCM;
// OR RUN LINE-BY-LINE FROM THE COMMAND LINE
// TWO STAGES DEPENDING ON THE REDISTRIBUTE SWITCH: 
// 	1. CONSTRUCT EQUATIONS, COMPILE MODEL, SIMULATE
// 	2. CONSTRUCT ADDFACTORS TO REDISTRIBUTE RESIDUAL, SIMULATE
// REVISIT WORKFLOW, SMALL DISCREPANCY BETWEEN AREMOS AND GEKKO IN 2020-21
// CONSIDER PLOTTING DATA FROM COMPARISON BANKS DIRECTLY INSTEAD OF WORK AND REF.
// *************************




TELL '*************************';
TELL ' SETUP                   ';
TELL '*************************';

// CLOSE BANKS AND CLEAR MEMORY AND SCREEN
RESTART;
CUT;
CLS;
// SET FONT SIZE IN GEKKO
OPTION INTERFACE ZOOM = 120;
// SET WORKING DIRECTORY, AND SUBFOLDERS 
// (IF YOU USE SINGLE QUOTES, HAVE TO PROVIDE ABSOLUTE PATH)
GLOBAL %WORK_DIR, %SAVE_EQ, %BNK_START, %BNK_END, %CURR_YR, %REDISTRIBUTE;
%WORK_DIR = 'I:\FORECAST\GEKKO';
OPTION FOLDER WORKING = %WORK_DIR;
OPTION FOLDER COMMAND =  '{%WORK_DIR}\CMD';
OPTION FOLDER BANK = '{%WORK_DIR}\DATA';
OPTION FOLDER MODEL =  '{%WORK_DIR}\MOD';
OPTION FOLDER PIPE = '{%WORK_DIR}\MOD';

// LOAD FUNCTIONS
RUN LIB.GCM;

// SET UP PERIODS ETC.
%BNK_START = 1900;
%BNK_END = 2060;
%CURR_YR = CURRENTYEAR();
TIME %BNK_START %BNK_END;

// SAVE EQUATIONS INTO FORMULA FILE?
%SAVE_EQ = 'Y';
// ONLY REDISTRIBUTE EXCESS E_ELSE AFTER MODEL WAS SIMULATED ONCE
// AND ADDFACTORS WERE UPDATED ACCORDINGLY
%REDISTRIBUTE = 'Y';


TELL '*************************';
TELL ' LOAD DATA INTO BANKS    ';
TELL '*************************';

// LOAD RELEVANT HISTORY (REPLACE @ WITH _ IN TSD FILES)
#TSD_LIST = ('BEA_A', 'BEA_Q', 'BLS_A', 'BLS_Q');
FOR STRING %TSD_I = #TSD_LIST;
IMPORT <ALL TSD> '\DATA\{%TSD_I}.TSD';
END;

// SAVE SERIES IN CNTY_DAT.GBK
INDEX *:*!* TO #CNTY_LIST;
WRITE {#CNTY_LIST} FILE=CNTY_DAT;

CLEAR;

// LOAD RELEVANT EXOGENOUS FORECAST
#TSD_LIST = ('ASOLX', 'QSOLX');
FOR STRING %TSD_I = #TSD_LIST;
IMPORT <ALL TSD> '\DATA\{%TSD_I}.TSD';
END;

// SAVE SERIES IN CNTY_EXOG.GBK
INDEX *:*!* TO #CNTY_LIST;
WRITE {#CNTY_LIST} FILE=CNTY_EXOG;

CLEAR;


TELL '*************************';
TELL ' STAGE 1                 ';
TELL '*************************';

// STAGE 1. CONSTRUCT EQUATIONS, COMPILE MODEL, SIMULATE

// HISTORY SAMPLE HAS TO BE LOADED
// LAST AVAILABLE OBSERVATION WILL DETERMINE MODEL COEFFICIENTS, SO CHECK SAMPLE
// CLEAR WORK AND REF BANKS AND READ SERIES INTO BANKS
READ 'CNTY_DAT';

// FOR PSEUDO-OUT-OF-SAMPLE EXERCISE DELETE OBSERVATIONS IN THE PSEUDO "FUTURE"
// FOR STRING %SER_I = #CNTY_LIST;
// {%SER_I} <2001 %BNK_END> = M();
// END;

// CALCULATE COEFFICIENT VALUES AND STORE EQUATIONS IN 'CNTY_EQ.FRM'
RUN EQUCA.GCM;

// COMPILE MODEL
MODEL CNTY_EQ;

// CLEAR WORK AND REF BANKS AND READ SERIES INTO BANKS
READ <MERGE> 'CNTY_EXOG';

// DELETE NON-MODEL SERIES;
DELETE<NONMODEL>;

// CLEAR OUT SIMULATION PERIOD FOR ENDOGENOUS SERIES (#ENDO)
// (FOR PSEUDO-OUT-OF-SAMPLE EXERCISE DELETE OBSERVATIONS IN THE PSEUDO "FUTURE")
// FOR STRING %SER_I = #ENDO;
// {%SER_I} <2001 %BNK_END> = M();
// END;

// SIMULATE THE MODEL OVER THE GIVEN PERIOD
SIM <2000 2050>;

// PLOT A COMPARISON OF SIMULATION TO ACTUAL
FOR STRING %SER_I = #ENDO;
PLOT <1990 2050> {%SER_I}<TYPE=LINES DASHTYPE='1'>, @{%SER_I}<TYPE=LINES DASHTYPE='2'>, PCH({%SER_I})<TYPE=BOXES Y2>, PCH(@{%SER_I})<TYPE=BOXES Y2> FILE='{%WORK_DIR}\FIGURES\{%SER_I}_SIMACT.pdf';
END;


TELL '*************************';
TELL ' STAGE 2                 ';
TELL '*************************';

// STAGE 2. REDISTRIBUTE RESIDUAL
IF (%REDISTRIBUTE=='Y');

// REDISTRIBUTION OF EXCESS RESIDUALS (E_ELSE) INTO EACH INDUSTRY IN EACH COUNTY.
RUN REDISTCA.GCM;

// SIMULATE THE MODEL OVER THE GIVEN PERIOD
SIM <2000 2050>;

// COPY HISTORY INTO FORECAST SERIES
FOR STRING %SER_I = #ENDO;
%LAST_PER = FROMSERIES(@{%SER_I}, 'DATAEND');
{%SER_I} <%BNK_START %LAST_PER> = @{%SER_I};
END;

// SAVE FORECAST IN BANK AND CLONE IT TO REF BANK
// INDEX *!* TO #FCAST_LIST;
// DISP {#EXT_LIST};
WRITE {#ENDO} FILE=CNTY_FCST;
CLONE;

// LOAD AREMOS FORECAST INTO WORK BANK
CLEAR WORK;
IMPORT <ALL TSD> '\DATA\CASOL_AREMOS.TSD';

// PLOT A COMPARISON OF SIMULATION TO ACTUAL
FOR STRING %SER_I = #ENDO;
PLOT <1990 2050> {%SER_I}<TYPE=LINES DASHTYPE='1'>, @{%SER_I}<TYPE=LINES DASHTYPE='2'>, PCH({%SER_I})<TYPE=BOXES Y2>, PCH(@{%SER_I})<TYPE=BOXES Y2> FILE='{%WORK_DIR}\FIGURES\{%SER_I}_GEKARE.pdf';
END;

END; // END IF


TELL '*************************';
TELL ' CASOL END               ';
TELL '*************************';


